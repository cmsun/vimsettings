
" =============================================================================
"        << 判断操作系统是 Windows 还是 Linux 和判断是终端还是 Gvim >>
" =============================================================================

" -----------------------------------------------------------------------------
"  < 设置操作系统是 Windows 还是 Linux >
" -----------------------------------------------------------------------------
if has("win16") || has("win32") || has("win64")
    let g:isWindows = 1
    let g:isLinux = 0
else
    let g:isWindows = 0
    let g:isLinux = 1
endif
" -----------------------------------------------------------------------------
"  < 判断是终端还是 Gvim >
" -----------------------------------------------------------------------------
if has("gui_running")
    let g:isGUI = 1
else
    let g:isGUI = 0
endif

" =============================================================================
"                          << 以下为软件默认配置 >>
" =============================================================================
" -----------------------------------------------------------------------------
"  < Windows Gvim 默认配置> 做了一点修改
" -----------------------------------------------------------------------------
if g:isWindows
    set nocompatible
    source $VIMRUNTIME/vimrc_example.vim
    source $VIMRUNTIME/mswin.vim  "用ctrl+a全选 ctrl+c复制 ctrl+v粘贴 (会使ctrl+v的块选择无效)
    behave mswin

    set diffexpr=MyDiff()
    function MyDiff()
        let opt = '-a --binary '
        if &diffopt =~ 'icase' | let opt = opt . '-i ' | endif
        if &diffopt =~ 'iwhite' | let opt = opt . '-b ' | endif
        let arg1 = v:fname_in
        if arg1 =~ ' ' | let arg1 = '"' . arg1 . '"' | endif
        let arg2 = v:fname_new
        if arg2 =~ ' ' | let arg2 = '"' . arg2 . '"' | endif
        let arg3 = v:fname_out
        if arg3 =~ ' ' | let arg3 = '"' . arg3 . '"' | endif
        let eq = ''
        if $VIMRUNTIME =~ ' '
            if &sh =~ '\<cmd'
                let cmd = '""' . $VIMRUNTIME . '\diff"'
                let eq = '"'
            else
                let cmd = substitute($VIMRUNTIME, ' ', '" ', '') . '\diff"'
            endif
        else
            let cmd = $VIMRUNTIME . '\diff'
        endif
        silent execute '!' . cmd . ' ' . opt . arg1 . ' ' . arg2 . ' > ' . arg3 . eq
    endfunction
endif
" -----------------------------------------------------------------------------
"  < Linux Gvim/Vim 默认配置> 做了一点修改
" -----------------------------------------------------------------------------
if g:isLinux
    set hlsearch        "高亮搜索
    set incsearch       "在输入要搜索的文字时，实时匹配

    " Uncomment the following to have Vim jump to the last position when
    " reopening a file
    if has("autocmd")
        au BufReadPost * if line("'\"") > 1 && line("'\"") <= line("$") | exe "normal! g'\"" | endif
    endif

    if g:isGUI
        " Source a global configuration file if available
        if filereadable("/etc/vim/gvimrc.local")
            source /etc/vim/gvimrc.local
        endif
    else
        " This line should not be removed as it ensures that various options are
        " properly set to work with the Vim-related packages available in Debian.
        runtime! debian.vim

        " Vim5 and later versions support syntax highlighting. Uncommenting the next
        " line enables syntax highlighting by default.
        if has("syntax")
            syntax on
        endif

        set mouse=a                    " 在任何模式下启用鼠标
        set t_Co=256                   " 在终端启用256色
        set backspace=2                " 设置退格键可用

        " Source a global configuration file if available
        if filereadable("/etc/vim/vimrc.local")
            source /etc/vim/vimrc.local
        endif
    endif
endif

" =============================================================================
"                          << 以下为用户自定义配置 >>
" =============================================================================
"  < 编码配置 >
" -----------------------------------------------------------------------------
" 注：使用utf-8格式后，软件与程序源码、文件路径不能有中文，否则报错
" -----------------------------------------------------------------------------
if has("multi_byte") 
    " UTF-8 编码 
    set encoding=utf-8 
    set termencoding=utf-8 
    set fileencoding=utf-8
    set fileencodings=utf-8,ucs-bom,chinese,cp936
    set formatoptions+=mM 
    set fencs=utf-8,gbk 
    if v:lang =~? '^/(zh/)/|/(ja/)/|/(ko/)' 
        set ambiwidth=double 
    endif 
    if has("win32") 
        source $VIMRUNTIME/delmenu.vim 
        source $VIMRUNTIME/menu.vim 
        language messages zh_CN.UTF-8 
    endif 
else 
    echoerr "Sorry, this version of (g)vim was not compiled with +multi_byte" 
endif

if (g:isWindows && g:isGUI)
    "解决菜单乱码
    source $vimruntime/delmenu.vim
    source $vimruntime/menu.vim
else
    "解决consle输出乱码
    language messages zh_CN.UTF-8
endif

" windows 下解决 Quickfix 乱码问题 
" windows 默认编码为 cp936，而 Gvim(Vim) 内部编码为 utf-8，所以常常输出为乱码
" 以下代码可以将编码为 cp936 的输出信息转换为 utf-8 编码，以解决输出乱码问题
" 但好像只对输出信息全部为中文才有满意的效果，如果输出信息是中英混合的，那可能
" 不成功，会造成其中一种语言乱码，输出信息全部为英文的好像不会乱码
" 如果输出信息为乱码的可以试一下下面的代码，如果不行就还是给它注释掉
if g:isWindows
    function QfMakeConv()
        let qflist = getqflist()
        for i in qflist
           let i.text = iconv(i.text, "cp936", "utf-8")
        endfor
        call setqflist(qflist)
    endfunction
    au QuickfixCmdPost make call QfMakeConv()
endif

" -----------------------------------------------------------------------------
filetype on                                           "启用文件类型
filetype plugin on                                    "根据文件类型加载相应的插件 
filetype indent on                                    "启用缩进
set fileformat=unix                                   "设置新（当前）文件的<EOL>格式，可以更改，如：dos（windows系统常用）
set fileformats=unix,dos,mac                          "给出文件的<EOL>格式类型
set nocompatible                                      "禁用 Vi 兼容模式
set cindent                                           "启用c风格对齐方式
set expandtab                                         "将Tab键转换为空格
set tabstop=4                                         "设置Tab键的宽度，可以更改，如：宽度为2
set shiftwidth=4                                      "换行时自动缩进宽度，可更改（宽度同tabstop）
set softtabstop=4                                     "使得按退格键时可以一次删除4个空格
" set foldenable                                        "启用折叠
" set foldmethod=indent                                 "indent 折叠方式
" set foldmethod=marker                               "marker 折叠方式
set writebackup                                       "保存文件前建立备份，保存成功后删除该备份
set nobackup                                          "设置无备份文件
set noswapfile                                        "设置无临时文件
set ignorecase                                        "搜索模式里忽略大小写
set smartcase                                         "如果搜索模式包含大写字符，不使用 'ignorecase' 选项，只有在输入搜索模式并且打开 'ignorecase' 选项时才会使用
" set noincsearch                                     "在输入要搜索的文字时，取消实时匹配
set autoread                                          "当文件在外部被修改，自动更新该文件
set autochdir                                         "自动切换到当前编辑文件的路径 
set number                                            "显示行号
set laststatus=2                                      "启用状态栏信息
set cmdheight=2                                       "设置命令行的高度为2，默认为1
set cursorline                                        "突出显示当前行
set nowrap                                            "设置不自动换行
set shortmess=atI                                     "去掉欢迎界面

" Linux没有Consolas字体，要到Windows系统的C:\Windows\Fonts目录里拷贝Consola.ttf字体文件
" 在Linux中双击字体文件Consola.ttf,然后打开字体浏览窗口，单击右上角的“安装”按钮进行安装
" 设置字体时等号两边不能有空格如：set guifont = Droid\ Sans\ Mono\ 10 这样写是错的
" 而 set guifont=Droid\ Sans\ Mono\ 10 这样写是正确的 
if g:isGUI
    " au GUIEnter * simalt ~x                         "窗口启动时自动最大化
    winpos 600 200                                    "指定窗口出现的位置，坐标原点在屏幕左上角
    set lines=120 columns=120                         "指定窗口大小，lines为高度，columns为宽度
    if g:isWindows
        set guifont=Consolas:h11                          
    else
        set guifont=Consolas\ 11
    endif
endif

" 启用每行超过80列的字符提示（字体变蓝并加下划线），不启用就注释掉
au BufWinEnter * let w:m2=matchadd('Underlined', '\%>' . 80 . 'v.\+', -1)

"设置代码配色方案         
colorscheme Tomorrow-Night-Eighties                    

"让Vim的补全菜单行为与一般IDE一致(参考VimTip1228)
set completeopt=longest,menu   

" " 自动切换目录为当前编辑文件所在目录
" au BufRead,BufNewFile,BufEnter * cd %:p:h
" " 离开插入模式后自动关闭预览窗口
" autocmd InsertLeave * if pumvisible() == 0|pclose|endif

" -----------------------------------------------------------------------------
" < 显示/隐藏菜单栏、工具栏、滚动条，可用 Ctrl + F11 切换 >
" -----------------------------------------------------------------------------
if g:isGUI
    set guioptions-=m
    set guioptions-=T
    set guioptions-=r
    set guioptions-=L
    nmap <silent> <c-F11> :if &guioptions =~# 'm' <Bar>
        \set guioptions-=m <Bar>
        \set guioptions-=T <Bar>
        \set guioptions-=r <Bar>
        \set guioptions-=L <Bar>
    \else <Bar>
        \set guioptions+=m <Bar>
        \set guioptions+=T <Bar>
        \set guioptions+=r <Bar>
        \set guioptions+=L <Bar>
    \endif<CR>
endif

" -----------------------------------------------------------------------------
"  < gvimfullscreen 工具配置 > 请确编译时加上了该功能
" -----------------------------------------------------------------------------
" 用于 Windows Gvim 全屏窗口，可用 F11 切换
" 全屏后再隐藏菜单栏、工具栏、滚动条效果更好
nmap <F11> <Esc>:call libcallnr("gvimfullscreen.dll", "ToggleFullScreen", 0)<CR>

" -----------------------------------------------------------------------------
"  < vimtweak 工具配置 > 请确保编译时加上了该功能
" -----------------------------------------------------------------------------
" 这里只用于窗口透明与置顶
" 常规模式下 Ctrl + Up（上方向键） 增加不透明度，Ctrl + Down（下方向键） 减少不透明度，<Leader>t 窗口置顶与否切换
if (g:isWindows && g:isGUI)
    let g:Current_Alpha = 255
    let g:Top_Most = 0
    func! Alpha_add()
        let g:Current_Alpha = g:Current_Alpha + 10
        if g:Current_Alpha > 255
            let g:Current_Alpha = 255
        endif
        call libcallnr("vimtweak.dll","SetAlpha",g:Current_Alpha)
    endfunc
    func! Alpha_sub()
        let g:Current_Alpha = g:Current_Alpha - 10
        if g:Current_Alpha < 155
            let g:Current_Alpha = 155
        endif
        call libcallnr("vimtweak.dll","SetAlpha",g:Current_Alpha)
    endfunc
    func! Top_window()
        if  g:Top_Most == 0
            call libcallnr("vimtweak.dll","EnableTopMost",1)
            let g:Top_Most = 1
        else
            call libcallnr("vimtweak.dll","EnableTopMost",0)
            let g:Top_Most = 0
        endif
    endfunc

    "快捷键设置
    nmap <c-up> :call Alpha_add()<CR>
    nmap <c-down> :call Alpha_sub()<CR>
    nmap <leader>t :call Top_window()<CR>
endif

" -----------------------------------------------------------------------------
"                <常用键盘映射>
" -----------------------------------------------------------------------------
"Ctrl+[代替Esc键
imap <c-[> <Esc>

"设置<leader>为","
let g:mapleader = ","

" 常规模式下用空格键来开关光标行所在折叠（注：zR 展开所有折叠，zM 关闭所有折叠）
nnoremap <space> @=((foldclosed(line('.')) < 0) ? 'zc' : 'zo')<CR>

"Ctrl+h 插入模式下光标向左移动
inoremap <c-h> <Left>
"Ctrl+j 插入模式下光标向下移动
inoremap <c-j> <Down>
"Ctrl+k 插入模式下光标向上移动
inoremap <c-k> <Up>
"Ctrl+l 插入模式下光标向右移动
inoremap <c-l> <Right>

func Cancel()
    let l:i = 0 
    let curline = getline('.')
    while(curline[-1:] != '.' && curline[-1:] != '>')
        let l:i = l:i + 1
        if curline == ''
            return
        endif
        let curline = curline[:-2] "删除最后一个字符
    endwhile
    while l:i >= 0
        call feedkeys("x" , 't')
        let l:i = l:i - 1
    endwhile
    call feedkeys("a"), 't'
endfunc

command! CancelComplete call Cancel()

"补全时Ctrl+j向下选择（选择而不选中）
" inoremap <c-j> <Down>
"补全时Ctrl+k向上选择（选择而不选中）
" inoremap <c-k> <Up>
inoremap <expr> <TAB> pumvisible() ? "\<Down>" : "\<TAB>"                     "补全时TAB向下选择（选择却不选中）
inoremap <expr> <CR>  pumvisible() ? "\<C-y>"  : "\<CR>"                      "补全时回车选中当前项
inoremap <expr> <c-z> pumvisible() ? "\<Esc>:CancelComplete\<CR>" : "\<c-z>"  "取消补全

" -----------------------------------------------------------------------------
"获取当前编辑的文件的后缀名
let s:suffix = expand("%:e")
"运行自己写的vim脚本，设置C、CPP、python等的头文件路径
runtime! plugin/SetPath.vim
" -----------------------------------------------------------------------------

" -----------------------------------------------------------------------------
"  < ctags 工具配置 >
" -----------------------------------------------------------------------------
" 对浏览代码非常的方便,可以在函数,变量之间跳转等
" --c++-kinds=+p  : 为C++文件增加函数原型的标签：
" --fields=+iaS   : 在标签文件中加入继承信息(i)、类成员的访问控制信息(a)、以及函数的指纹(S)
" --extra=+q      : 为标签增加类修饰符。注意，如果没有此选项，将不能对类成员补全 

"生成当前工程的tags文件：
    map <leader>c1 :!ctags --c++-kinds=+p --fields=+iaS --extra=+q -f tags -R `pwd`<CR><CR> :TlistUpdate<CR>
"生成c/cpp系统tags文件：
    map <leader>c2 :!ctags --c++-kinds=+p --fields=+iaS --extra=+q -f $FOLDER_PATH/systags -R $SYS_PATH <CR><CR> :TlistUpdate<CR>
"生成stl模板的tags文件:
"下面这个命令要加上--language-force=c++ 是把所有文件强行当成c++文件，否则STL中"的不带后缀名的文件不会生成c++的信息文件
    map <leader>c3 :!ctags --c++-kinds=+p --fields=+iaS --extra=+q --language-force=c++ -f $FOLDER_PATH/stltags -R $CPP_PATH <CR><CR> :TlistUpdate<CR>
"生成qt的tags文件
    map <leader>c4 :!ctags --c++-kinds=+p --fields=+iaS --extra=+q --language-force=c++ -f $FOLDER_PATH/qttags -R $QT_PATH <CR><CR> :TlistUpdate<CR>
"生成当前python工程tags文件
    map <leader>c5 :!ctags --fields=+iaS --extra=+q -f tags -R `pwd` <CR><CR> :TlistUpdate<CR>
"生成python2系统tags文件
    map <leader>c6 :!ctags --fields=+iaS --extra=+q -f $FOLDER_PATH/py2tags -R $PYTHON2_PATH <CR><CR> :TlistUpdate<CR>
"生成python3系统tags文件
    map <leader>c7 :!ctags --fields=+iaS --extra=+q -f $FOLDER_PATH/py3tags -R $PYTHON3_PATH <CR><CR> :TlistUpdate<CR>

if( s:suffix == "c" || s:suffix == "cpp" ||
  \ s:suffix == "h" || s:suffix == "hpp" )
    set tags+=tags                           
    set tags+=$FOLDER_PATH/systags
    set tags+=$FOLDER_PATH/stltags
    set tags+=$FOLDER_PATH/qttags
elseif( s:suffix == "py" ) 
    set tags+=tags
    set tags+=$FOLDER_PATH/py2tags
    set tags+=$FOLDER_PATH/py3tags
endif

" -----------------------------------------------------------------------------
"  < cscope 工具配置 >
" -----------------------------------------------------------------------------
" 生成cscope.files文件后如果源文件路径中有空格，则在vim中执行替换命令：%s/^\|$/"/g 把行首和行尾用引号括起来
" 但现今版本的cscope如果源路径有空格即使用双引号括起cscope.files中的每一行，并成功生成cscope.out文件，但依然
" 无法成功使用cscope功能,只有源文件路径中没有空格可以正常使用cscope。

" find 'C:\Qt\Qt5.2.1\Tools\mingw48_32\lib\gcc\i686-w64-mingw32\4.8.0\include' -type f -name "*" > D:/TagsFiles/syscscope.files
" cscope -Rbq -i D:/TagsFiles/syscscope.files -f D:/TagsFiles/syscscope.out         

" find 'C:\Qt\Qt5.2.1\5.2.1\mingw48_32\include' -type f -name "*" > D:/TagsFiles/qtcscope.files
" cscope -Rbq -i D:/TagsFiles/qtcscope.files -f D:/TagsFiles/qtcscope.out         

" find 'C:\Python27\Lib' -type f -name "*.py" > D:/TagsFiles/py2cscope.files
" cscope -Rbq -i D:/TagsFiles/py2cscope.files -f D:/TagsFiles/py2cscope.out      

" find 'C:\Python34\Lib' -type f -name "*.py" > D:/TagsFiles/py3cscope.files
" cscope -Rbq -i D:/TagsFiles/py3cscope.files -f D:/TagsFiles/py3cscope.out      

"find /usr/include -type f -name "*" > ~/.vim/TagsFiles/syscscope.files
"cscope -Rbq -i ~/.vim/cscope_data/syscscope.files -f ~/.vim/TagsFiles/syscscope.out

"find /opt/Qt5.2.1/5.2.1/gcc_64/include -type f -name "*" > ~/.vim/TagsFiles/qtcscope.files
"cscope -Rbq -i ~/.vim/TagsFiles/qtcscope.files -f ~/.vim/TagsFiles/qtcscope.out

"find /usr/lib/python2.7 -type f -name "*.py" > ~/.vim/TagsFiles/py2cscope.files
"cscope -Rbq -i ~/.vim/TagsFiles/py2cscope.files -f ~/.vim/TagsFiles/py2cscope.out

"find /usr/lib/python3.4 -type f -name "*.py" > ~/.vim/TagsFiles/py3cscope.files
"cscope -Rbq -i ~/.vim/TagsFiles/py3cscope.files -f ~/.vim/TagsFiles/py3cscope.out

" 生成当前工程cscope数据库：
" find `pwd` -name "*.[ch]" -o -name "*.[ch]pp" -o -name "*.py" > cscope.files
" cscope -Rbq -i cscope.files

" windows中，命令find -name "*" 放在func函数中时，如果双引号前不加转义符，时常会出错
func CsSys() 
    if g:isWindows
        :!find $SYS_PATH -type f -name \"*\" > $FOLDER_PATH/syscscope.files
    else
        :!find $SYS_PATH -type f -name "*" > $FOLDER_PATH/syscscope.files
    endif
    :!cscope -Rbq -i $FOLDER_PATH/syscscope.files -f $FOLDER_PATH/syscscope.out
endfunc

func CsQt()
    if g:isWindows
        :!find $QT_PATH -type f -name \"*\" > $FOLDER_PATH/qtcscope.files
    else
        :!find $QT_PATH -type f -name "*" > $FOLDER_PATH/qtcscope.files
    endif
    :!cscope -Rbq -i  $FOLDER_PATH/qtcscope.files -f $FOLDER_PATH/qtcscope.out
endfunc

func CsPy()
    if g:isWindows
        :!find $PYTHON2_PATH -type f -name \"*.py\" >  $FOLDER_PATH/py2cscope.files
        :!find $PYTHON3_PATH -type f -name \"*.py\" >  $FOLDER_PATH/py3cscope.files
    else
        :!find $PYTHON2_PATH -type f -name "*.py" >  $FOLDER_PATH/py2cscope.files
        :!find $PYTHON3_PATH -type f -name "*.py" >  $FOLDER_PATH/py3cscope.files
    endif
    :!cscope -Rbq -i  $FOLDER_PATH/py2cscope.files -f  $FOLDER_PATH/py2cscope.out
    :!cscope -Rbq -i  $FOLDER_PATH/py3cscope.files -f  $FOLDER_PATH/py3cscope.out
endfunc

func CsPrj()
    if g:isWindows
        :!find `pwd` -name \"*.[ch]\" -o -name \"*.[ch]pp\" -o -name \"*.py\" > cscope.files
    else
        :!find `pwd` -name "*.[ch]" -o -name "*.[ch]pp" -o -name "*.py" > cscope.files
    endif
    :!cscope -Rbq -i cscope.files
endfunc

command! Cssys call CsSys()
command! Csqt call CsQt()
command! Cspy call CsPy()
command! Csprj call CsPrj()

if( (s:suffix == "c" || s:suffix == "cpp" ||
   \ s:suffix == "h" || s:suffix == "hpp" || 
   \ s:suffix == "py") && has("cscope"))

    "添加系统cscope数据库
    if s:suffix != "py" 
        cs add $FOLDER_PATH/syscscope.out
        cs add $FOLDER_PATH/qtcscope.out
    elseif s:suffix == "py" 
            cs add $FOLDER_PATH/py2cscope.out
            cs add $FOLDER_PATH/py3cscope.out
    endif

    "添加当前工程cscope数据库
    if filereadable("cscope.out")
        cs add cscope.out
    endif

    "设定可以使用quickfix窗口来查看cscope结果(命令copen打开quickfix窗口查看cscope结果,命令cclose关闭窗口)
    set cscopequickfix=s-,c-,d-,i-,t-,e-
    " 使用 Ctrl+]  和 Ctrl+t 时同时搜索tags标签和cscope数据库 
    set cst
    "使用 Ctrl+]  和 Ctrl+t 时先搜索tags标签，失败后再搜索cscope数据库
    set csto=1
    "提示是否成功加载cscope数据库
    set cscopeverbose

    "快捷键设置
    nmap <C-\>s :cs find s <C-R>=expand("<cword>")<CR><CR>
    nmap <C-\>g :cs find g <C-R>=expand("<cword>")<CR><CR>
    nmap <C-\>c :cs find c <C-R>=expand("<cword>")<CR><CR>
    nmap <C-\>t :cs find t <C-R>=expand("<cword>")<CR><CR>
    nmap <C-\>e :cs find e <C-R>=expand("<cword>")<CR><CR>
    nmap <C-\>f :cs find f <C-R>=expand("<cfile>")<CR><CR>
    nmap <C-\>i :cs find i ^<C-R>=expand("<cfile>")<CR><CR>
    nmap <C-\>d :cs find d <C-R>=expand("<cword>")<CR><CR>
endif

" -----------------------------------------------------------------------------
"  < vundle 插件管理工具配置 >
" -----------------------------------------------------------------------------
" 用于更方便的管理vim插件，具体用法参考 :h vundle 帮助
" Vundle工具安装方法为在终端输入如下命令
" git clone https://github.com/gmarik/vundle.git ~/.vim/bundle/vundle

if g:isWindows 
    set rtp+=$VIM/bundle/vundle
    call vundle#rc('$VIM/bundle/')
else
    set rtp+=~/.vim/bundle/vundle
    call vundle#rc()
endif

"----vundle管理插件，必须要先有这个插件---
Bundle 'gmarik/vundle'

"-----------补全插件----------------------
if( s:suffix == "c" || s:suffix == "cpp" ||
  \ s:suffix == "h" || s:suffix == "hpp" || 
  \ s:suffix == "py" )
    Bundle 'Valloric/YouCompleteMe'
endif

"补全() "" ''等的插件。会占用<c-h>使其作为删除键。
"解决方法为打开auto-pairs.vim文件，注释掉455行，即下面这行：
"execute 'inoremap <buffer> <silent> <C-h> <C-R>=AutoPairsDelete()<CR> 
Bundle 'jiangmiao/auto-pairs'  
"snipmate会占用TAB键使TAB键的自定义映射失效
Bundle 'msanders/snipmate.vim'  

"--------语法检查和调试插件--------------
Bundle 'scrooloose/syntastic'

"----代码浏览和文件浏览插件--------------
"在头文件和c文件之间跳转
Bundle 'a.vim'
"浏览头文件
Bundle 'ctrlpvim/ctrlp.vim'
"当前工程文件浏览插件
Bundle 'scrooloose/nerdtree'
"代码浏览器
Bundle 'taglist.vim'
"代码浏览器(比taglist更好地支持面向对向)
Bundle 'majutsushi/tagbar'
"增加纯文本的浏览效率
Bundle 'TxtBrowser'
"快速地在缓存中切换
Bundle 'BufExplorer.zip'
"分割窗口的最大化最小化
Bundle 'ZoomWin'

"----------对齐插件和注释插件------------
"显示代码之间的缩进对齐
Bundle 'Yggdroot/indentLine'
"对代码进行对齐
Bundle 'Align'
"注释插件
Bundle 'scrooloose/nerdcommenter'

"-----------高亮插件---------------------
Bundle 'std_c.zip'
Bundle 'Mark--Karkat'
Bundle 'cSyntaxAfter'

"显示状态
Bundle 'Lokaltog/vim-powerline'
"重复命令
Bundle 'repeat.vim'

"------------------------------------------------------------------------------
"   < YouCompleteMe配置 >
"------------------------------------------------------------------------------
if g:isWindows
    let s:defaultconf = $VIM.'\bundle\YouCompleteme\python\.ycm_extra_conf.py'          "小数点用来连接两个字符串,vim插件的语法参照eval.cnx文件
else
    let s:defaultconf = $HOME.'/.vim/bundle/YouCompleteMe/python/.ycm_extra_conf.py'            
endif
let g:ycm_global_ycm_extra_conf=s:defaultconf                                           "设置默认的ycm_extra_conf.py的路径 
let g:ycm_confirm_extra_conf=0                                                          "打开vim时不再询问是否加载ycm_extra_conf.py配置
let g:ycm_collect_identifiers_from_tags_files=1                                         "开启 YCM 基于标签引擎,使用tags文件里的标签进行补全
let g:ycm_complete_in_comments=1                                                        "在注释输入中也能补全
let g:ycm_complete_in_strings=1                                                         "在字符串输入中也能补全
let g:ycm_collect_identifiers_from_comments_and_strings=0                               "注释和字符串中的文字也会被收入补全
let g:ycm_min_num_of_chars_for_completion=2                                             "从第2个键入字符就开始罗列匹配项
let g:ycm_cache_omnifunc=0                                                              "禁止缓存匹配项,每次都重新生成匹配项
let g:ycm_seed_identifiers_with_syntax=1                                                "语法关键字补全
let g:ycm_key_invoke_completion='<c-\>'                                                 "ctrl+\ 补全c函数和宏
let g:ycm_error_symbol = '☆'                                                            "语法错误指示标志
let g:ycm_warning_symbol = '※'                                                          "警告指示标志

nnoremap <leader>lo :lopen<CR>                                                          "open locationlist
nnoremap <leader>lc :lclose<CR>                                                         "close locationlist

nnoremap <F5> :YcmDiags<CR>                                                             "编译并检查语法，如有语法错误会提示
nnoremap <leader>gd :YcmCompleter GoToDeclaration<CR>                                   "跳到声明处
nnoremap <leader>gf :YcmCompleter GoToDefinition<CR>                                    "跳到定义处
nnoremap <leader>gg :YcmCompleter GoToDefinitionElseDeclaration<CR>                     "两者结合

"取消默认的选择方式，使用自己定义的选择方式
let g:ycm_key_list_select_completion = ['<c-n>', '<Down>']                              "['<TAB>', '<Down>']是默认的向下补全选项
let g:ycm_key_list_previous_completion=['<c-p>', '<UP>']                                "['<S-TAB>', '<Up>']是默认的向上补全选项

" -----------------------------------------------------------------------------
"  < auto-pairs 插件配置 >
" -----------------------------------------------------------------------------
" 用于括号与引号自动补全，不过会与函数原型提示插件echofunc冲突
" 所以没有加入echofunc插件

" -----------------------------------------------------------------------------
"  < snipMate 插件配置 >
" -----------------------------------------------------------------------------
" 用于各种代码补全，这种补全是一种对代码中的词与代码块的缩写补全，详细用法可以参
" 考使用说明或网络教程等。不过有时候也会与 supertab 插件在补全时产生冲突

" -----------------------------------------------------------------------------
"  < Syntastic 插件配置 >
" -----------------------------------------------------------------------------
let g:syntastic_c_checkers=['make']
let g:syntastic_always_populate_loc_list = 1
let g:syntastic_auto_loc_list = 1
let g:syntastic_check_on_open=0
let g:syntastic_check_on_wq = 0
let g:syntastic_enable_signs=1
let g:syntastic_error_symbol='☆'
let g:syntastic_warning_symbol='※'
set statusline+=%#warningmsg#
set statusline+=%{SyntasticStatuslineFlag()}
set statusline+=%*gbar"

" -----------------------------------------------------------------------------
"  < a.vim 插件配置 >
" -----------------------------------------------------------------------------
" 用于切换C/C++头文件
" :A     ---切换头文件并独占整个窗口
" :AV    ---切换头文件并垂直分割窗口
" :AS    ---切换头文件并水平分割窗口

" -----------------------------------------------------------------------------
"  < ctrlp.vim 插件配置 >
" -----------------------------------------------------------------------------
" 一个全路径模糊文件，缓冲区，... 检索插件；详细帮助见 :h ctrlp
" 常规模式下输入：Ctrl + p 调用插件

" -----------------------------------------------------------------------------
"  < nerdtree 插件配置 >
" -----------------------------------------------------------------------------
" 树形目录结构文件浏览插件
" nmap <F2> :NERDTreeToggle<CR> " 常规模式下输入 F2 调用插件
"NERDTreeIgnore参数：忽略不显示的文件，方括号内是正则表达式，被匹配到的文件不会显示
let NERDTreeIgnore=['\..*\~']   
let NERDTreeIgnore+=['\.[^chp]']        "匹配不含 ".c" ".h" ".p"的文件
let NERDTreeIgnore+=['\.[ch][^p]']      "匹配含 ".c" ".h" 但是后面跟的不是"P"的文件 
let NERDTreeIgnore+=['\.[ch]p\{1\}$']   "匹配含 ".c" ".h" 后面跟的是"P",但是"p"只出现了一次的文件
let NERDTreeIgnore+=['\.[ch]p\{3,\}$']  "匹配含 ".c" ".h" 后面跟的是"P",但是"p"出现了三次及以上的文件
let NERDTreeIgnore+=['\.p[^y]$']        "匹配含 ".p" 但是后面跟的不是"y"的文件
let NERDTreeShowFiles = 1               "是否显示文件夹
let NERDTreeWinPos = 'right'            "窗口位置（'left' or ‘'right'）

" -----------------------------------------------------------------------------
"  < TagList 插件配置 >
" -----------------------------------------------------------------------------
" 高效地浏览源码, 其功能就像vc中的workpace
" 那里面列出了当前文件中的所有宏,全局变量, 函数名等

" 常规模式下输入 tl 调用插件，如果有打开 Tagbar 窗口则先将其关闭
nmap tl :TagbarClose<CR>:Tlist<CR>
let Tlist_Show_One_File=0                   "只显示当前文件的tags
let Tlist_Enable_Fold_Column=0              "使taglist插件不显示左边的折叠行
let Tlist_Exit_OnlyWindow=1                 "如果Taglist窗口是最后一个窗口则退出Vim
let Tlist_File_Fold_Auto_Close=1            "自动折叠
let Tlist_WinWidth=30                       "设置窗口宽度
let Tlist_Use_Left_Window=1                 "在左侧窗口中显示

" -----------------------------------------------------------------------------
"  < Tagbar 插件配置 >
" -----------------------------------------------------------------------------
" 相对 TagList 能更好的支持面向对象
" 常规模式下输入 tb 调用插件，如果有打开 TagList 窗口则先将其关闭
nmap tb :TlistClose<CR>:TagbarToggle<CR>
let g:tagbar_width=30                       "设置窗口宽度
let g:tagbar_left=1                         "在左侧窗口中显示

" -----------------------------------------------------------------------------
"  < txtbrowser 插件配置 >
" -----------------------------------------------------------------------------
" 用于文本文件生成标签与与语法高亮（调用TagList插件生成标签，如果可以）
au BufRead,BufNewFile *.txt setlocal ft=txt

" -----------------------------------------------------------------------------
"  < BufExplorer 插件配置 >
" -----------------------------------------------------------------------------
" 快速轻松的在缓存中切换（相当于另一种多个文件间的切换方式）
" <Leader>be 在当前窗口显示缓存列表并打开选定文件
" <Leader>bs 水平分割窗口显示缓存列表，并在缓存列表窗口中打开选定文件
" <Leader>bv 垂直分割窗口显示缓存列表，并在缓存列表窗口中打开选定文件

" -----------------------------------------------------------------------------
"  < ZoomWin 插件配置 >
" -----------------------------------------------------------------------------
" 用于分割窗口的最大化与还原
" 常规模式下按快捷键 <c-w>o 在最大化与还原间切换

" -----------------------------------------------------------------------------
"  < indentLine 插件配置 >
" -----------------------------------------------------------------------------
" 用于显示对齐线，与 indent_guides 在显示方式上不同，根据自己喜好选择了
" 在终端上会有屏幕刷新的问题，这个问题能解决就好了
" 开启/关闭对齐线
nmap <leader>il :IndentLinesToggle<CR>
" 设置Gvim的对齐线样式
if g:isGUI
    " let g:indentLine_char = "┊"
    let g:indentLine_first_char = "┊"
endif
" 设置终端对齐线颜色，如果不喜欢可以将其注释掉采用默认颜色
let g:indentLine_color_term = 239
" 设置 GUI 对齐线颜色，如果不喜欢可以将其注释掉采用默认颜色
let g:indentLine_color_gui = '#A4E57E'

" -----------------------------------------------------------------------------
"  < Align 插件配置 >
" -----------------------------------------------------------------------------
" 一个对齐的插件，用来——排版与对齐代码，功能强大，不过用到的机会不多

" -----------------------------------------------------------------------------
"  < nerdcommenter 插件配置 >
" -----------------------------------------------------------------------------
" 我主要用于C/C++代码注释(其它的也行)
" 以下为插件默认快捷键，其中的说明是以C/C++为例的，其它语言类似
" <leader>ci 以每行一个 /* */ 注释选中行(选中区域所在行)，再输入则取消注释
" <leader>cm 以一个 /* */ 注释选中行(选中区域所在行)，再输入则称重复注释
" <leader>cc 以每行一个 /* */ 注释选中行或区域，再输入则称重复注释
" <leader>cu 取消选中区域(行)的注释，选中区域(行)内至少有一个 /* */
" <leader>ca 在/*...*/与//这两种注释方式中切换（其它语言可能不一样了）
" <leader>cA 行尾注释
let NERDSpaceDelims = 1                     "在左注释符之后，右注释符之前留有空格

" -----------------------------------------------------------------------------
"  < std_c 插件配置 >
" -----------------------------------------------------------------------------
" 用于增强C语法高亮

" -----------------------------------------------------------------------------
"  < Mark--Karkat（也就是 Mark） 插件配置 >
" -----------------------------------------------------------------------------
" 给不同的单词高亮，表明不同的变量时很有用，详细帮助见 :h mark.txt

" -----------------------------------------------------------------------------
"  < cSyntaxAfter 插件配置 >
" -----------------------------------------------------------------------------
" 高亮括号与运算符等
au! BufRead,BufNewFile,BufEnter *.{c,cpp,h,java,javascript} call CSyntaxAfter()

" -----------------------------------------------------------------------------
"  < powerline 插件配置 >
" -----------------------------------------------------------------------------
" 状态栏插件

" -----------------------------------------------------------------------------
"  < repeat 插件配置 >
" -----------------------------------------------------------------------------
" 主要用"."命令来重复上次插件使用的命令

" -----------------------------------------------------------------------------
"   快速添加文件信息(添加函数信息请使用snipmate: comment+Tab)
" -----------------------------------------------------------------------------
func AddTitle()  
    call append( 0, "/*")
    call append( 1, "********************************************************************************")  
    call append( 2, "*                              COPYRIGHT NOTICE")  
    call append( 3, "*                             Copyright (c) 2016")  
    call append( 4, "*                             All rights reserved")  
    call append( 5, "*")  
    call append( 6, "*  @FileName       : ".expand("%:t"))  
    call append( 7, "*  @Author         : scm 351721714@qq.com")  
    call append( 8, "*  @Create         : ".strftime("%Y/%m/%d %H:%M:%S"))  
    call append( 9, "*  @Last Modified  : ".strftime("%Y/%m/%d %H:%M:%S"))  
    call append(10, "********************************************************************************")  
    call append(11, "*/")
    echohl WarningMsg | echo "Successful in adding the copyright." | echohl None  
endfunc  
"新建c、h、cpp、hpp文件时自动插入文件信息
autocmd BufNewFile *.[ch],*.[ch]pp, exec ":call AddTitle()"

func UpdateTitle()
    "保存光标位置
    normal m`
    call cursor(13,1)
    if search('*  @Last Modified  : ') != 0
        let line = line('.')
        call setline(line, '*  @Last Modified  : '.strftime("%Y/%m/%d %H:%M:%S"))
    endif
    "还原光标位置
    normal ``
endfunc
"保存文件时自动更新时间
autocmd FileWritePre,BufWritePre *.[ch],*.[ch]pp exec ":call UpdateTitle()"

func TestTitle()
    normal m`
    call cursor(13,1)
    if(search('*  @FileName       : ') == 0)
        call AddTitle()
    endif
    normal ``
endfunc
command! Title call TestTitle()
